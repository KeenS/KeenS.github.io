<!DOCTYPE html>
<html xmlns="http://www.w3.org/1999/xhtml" xml:lang="en" lang="en-us">
    <head>
        
        <meta http-equiv="content-type" content="text/html; charset=utf-8" />
        <meta name="viewport" content="width=device-width, initial-scale=1.0, maximum-scale=1" />

        <title>SML#に学ぶコンパイラ言語におけるREPLの実装方法 | κeenのHappy Hacκing Blog</title>
        <link rel="stylesheet" href='http://fonts.googleapis.com/css?family=Open+Sans:400,300,600' type='text/css' />
        <link rel="stylesheet" href="/libraries/normalize.3.0.1.css" />
        <link rel="stylesheet" href="/css/liquorice.css" />
        <link rel="shortcut icon" href="/favicon.ico" />
        <link rel="apple-touch-icon-precomposed" href="/apple-touch-icon-144-precomposed.png" sizes="144x144" />
        <link rel="alternate" href="" type="application/rss+xml" title="κeenのHappy Hacκing Blog" />
        
        <script>
         (function(i,s,o,g,r,a,m){i['GoogleAnalyticsObject']=r;i[r]=i[r]||function(){
             (i[r].q=i[r].q||[]).push(arguments)},i[r].l=1*new Date();a=s.createElement(o),
                                  m=s.getElementsByTagName(o)[0];a.async=1;a.src=g;m.parentNode.insertBefore(a,m)
         })(window,document,'script','//www.google-analytics.com/analytics.js','ga');

         ga('create', 'UA-43779888-1', 'auto');
         ga('send', 'pageview');

        </script>
        
        <link rel="stylesheet" href="/highlight.js/styles/xcode.css" />
<script src="/highlight.js/highlight.min.js"></script>
<script>hljs.initHighlightingOnLoad();</script>
<script type="text/x-mathjax-config">
 MathJax.Hub.Config({ tex2jax: { inlineMath: [['$','$'], ["\\(","\\)"]] } });
</script>
<script type="text/javascript"
        src="http://cdn.mathjax.org/mathjax/latest/MathJax.js?config=TeX-AMS_HTML">
</script>
<meta http-equiv="X-UA-Compatible" CONTENT="IE=EmulateIE7" />

    </head>
    <body class="li-body">

<header class="li-page-header">
    <div class="container">
        <div class="row">
            <div class="sixteen columns">
                <div class="li-brand li-left">
                <a href="/">κeenのHappy Hacκing Blog</a> | Lispエイリアンの狂想曲</div>
                <div class="li-menu li-right">
                    <span class="li-menu-icon" onclick="javascript:toggle('menu');">&#9776;</span>
                    <ul id="menu2" class="li-menu-items">
                        
                            <li><a href="/about/"> About </a></li>
                        
                            <li><a href="/index.xml"> Atom </a></li>
                        
                            <li><a href="/post/"> Blog </a></li>
                        
                            <li><a href="/slide/"> Slide </a></li>
                        
                    </ul>
                </div>
            </div>
        </div>
        <div class="row">
            <div class="sixteen columns">
                <ul id="menu" class="li-menu-items li-menu-mobile">
                    
                        <li><a href="/about/"> About </a></li>
                    
                        <li><a href="/index.xml"> Atom </a></li>
                    
                        <li><a href="/post/"> Blog </a></li>
                    
                        <li><a href="/slide/"> Slide </a></li>
                    
                </ul>
            </div>
        </div>
    </div>
</header>






    <div class="container">
        <div class="row">
            <div class="sixteen columns">
                <article class="li-article">
                    <header class="li-article-header">
                        <h1 class="li-article-title">SML#に学ぶコンパイラ言語におけるREPLの実装方法</h1>
                        <div class="li-article-meta">
    <time class="li-article-date">2015-12-12</time>
    <ul class="li-article-tag">
    
        <li>
            <a href="/categories/sml">SML</a>
        </li>
    
        <li>
            <a href="/categories/smlsharp">SMLSharp</a>
        </li>
    
        <li>
            <a href="/categories/advent-calendar">Advent Calendar</a>
        </li>
    
        <li>
            <a href="/categories/advent-calendar-2015">Advent Calendar 2015</a>
        </li>
    
        <li>
            <a href="/categories/%e8%a8%80%e8%aa%9e%e5%87%a6%e7%90%86%e7%b3%bb">言語処理系</a>
        </li>
    
</ul>

</div>

                    </header>
                    <section>
                        <p>このエントリは<a href="http://qiita.com/advent-calendar/2015/lang_dev">言語実装 Advent Calendar 2015</a>16日目の記事です。</p>

<p>κeenです。かねてより気になっていたREPLの実装方法について、SML#のソースコードを読んだのでその話でもします。

さて、ソースコードを読みながらやっていきましょう。SML# 2.0.0のソースコードです。</p>

<p>REPLのエントリポイントは<a href="https://github.com/smlsharp/smlsharp/blob/master/src/compiler/main/main/SimpleMain.sml#L949">src/compiler/main/main/SimpleMain.smlL949</a>です。</p>

<pre><code>      | Interactive (options, context) =&gt;
        let
          val newContext =
              Top.loadInteractiveEnv
                {stopAt = Top.NoStop,
                 stdPath = [#systemBaseDir options],
                 loadPath = nil}
                context
                (Filename.concatPath
                   (#systemBaseDir options, Filename.fromString &quot;prelude.smi&quot;))
          val context =
              let
                val context = Top.extendContext (context, newContext)
                val context = Top.incVersion context
              in
                context
              end
          val _ = ReifiedTermData.init (#topEnv context)
                  handle e =&gt; raise e
        in
          RunLoop.interactive options context;
          SUCCESS
        end

</code></pre>

<p>いろいろごちゃごちゃやってますがオプションと共に<code>RunLoop.interactive</code>を呼んでるだけです。</p>

<p>さて、RunLoop.smlはSimpleMain.smlと同じディレクトリにあります。<code>interactive</code>関数は次のように定義されています。</p>

<pre><code class="language-sml">  fun interactive options context =
      let
        ...
        val state = initInteractive ()
        fun loop context input =
            if !(#eof state) then ()
            else
              (Counter.reset();
               NameEvalEnv.intExnConList();
               case run options context input of
                 SUCCESS newContext =&gt;
                 ....
               | FAILED =&gt;
                 loop (Top.incVersion context) (interactiveInput state)
              )
      in
        loop context (interactiveInput state)
      end
</code></pre>

<p>何やら状態を初期化した後は<code>interactiveInput</code>で得られた結果を<code>run</code>に渡しているようです。<code>run</code>を見ましょう。</p>

<pre><code class="language-sml">  fun run ({stdPath, loadPath, LDFLAGS, LIBS, errorOutput, llvmOptions,
            ...}:options)
          context input =
      let
        fun puts s = TextIO.output (errorOutput, s ^ &quot;\n&quot;)
        val options = {stopAt = Top.NoStop,
                       baseFilename = NONE,
                       stdPath = stdPath,
                       loadPath = loadPath}
        val ({interfaceNameOpt, ...}, result) =
             Top.compile options context input
             handle e =&gt;
             (
               case e of
                 UserError.UserErrors errs =&gt;
                 app (fn e =&gt; puts (userErrorToString e)) errs
               | UserError.UserErrorsWithoutLoc errs =&gt;
                 app (fn (k,e) =&gt; puts (userErrorToString (Loc.noloc,k,e))) errs
               | Bug.Bug s =&gt; puts (&quot;Compiler bug:&quot; ^ s)
               | exn =&gt; raise exn;
               raise CompileError
            )
        val (newContext, module) =
            case result of
              Top.RETURN (newContext, module) =&gt; (newContext, module)
            | Top.STOPPED =&gt; raise Bug.Bug &quot;run&quot;
      in
        let
          val objfile = TempFile.create (&quot;.&quot; ^ SMLSharp_Config.OBJEXT ())
          val asmfile = TempFile.create (&quot;.&quot; ^ SMLSharp_Config.ASMEXT ())
          val _ = #start Counter.llvmOutputTimeCounter()
          val _ = LLVM.compile llvmOptions (module, LLVM.AssemblyFile,
                                            Filename.toString asmfile)
          val _ = LLVM.compile llvmOptions (module, LLVM.ObjectFile,
                                            Filename.toString objfile)
          val _ = #stop Counter.llvmOutputTimeCounter()
          val _ = LLVM.LLVMDisposeModule module
          val sofile = TempFile.create (SMLSharp_Config.DLLEXT ())
          val ldflags =
              case SMLSharp_Config.HOST_OS_TYPE () of
                SMLSharp_Config.Unix =&gt; nil
              | SMLSharp_Config.Cygwin =&gt;
                [&quot;-Wl,-out-implib,&quot;
                 ^ Filename.toString (Filename.replaceSuffix &quot;lib&quot; sofile)]
              | SMLSharp_Config.Mingw =&gt;
                [&quot;-Wl,--out-implib=&quot;
                 ^ Filename.toString (Filename.replaceSuffix &quot;lib&quot; sofile)]
          val libfiles =
              case SMLSharp_Config.HOST_OS_TYPE () of
                SMLSharp_Config.Unix =&gt; nil
              | SMLSharp_Config.Cygwin =&gt;
                map (fn x =&gt; Filename.toString (Filename.replaceSuffix &quot;lib&quot; x))
                    (!loadedFiles)
              | SMLSharp_Config.Mingw =&gt;
                map (fn x =&gt; Filename.toString (Filename.replaceSuffix &quot;lib&quot; x))
                    (!loadedFiles)
          val _ = BinUtils.link
                    {flags = SMLSharp_Config.RUNLOOP_DLDFLAGS () :: LDFLAGS
                             @ ldflags,
                     libs = libfiles @ LIBS,
                     objects = [objfile],
                     dst = sofile,
                     useCXX = false,
                     quiet = not (!Control.printCommand)}
          val so = DynamicLink.dlopen' (Filename.toString sofile,
                                        DynamicLink.GLOBAL,
                                        DynamicLink.NOW)
                   handle OS.SysErr (msg, _) =&gt; raise DLError msg
          val {mainSymbol, stackMapSymbol, codeBeginSymbol, ...} =
              GenerateMain.moduleName (interfaceNameOpt, #version context)
          val smap = DynamicLink.dlsym' (so, stackMapSymbol)
                     handle OS.SysErr (msg, _) =&gt; raise DLError msg
          val base = DynamicLink.dlsym' (so, codeBeginSymbol)
                     handle OS.SysErr (msg, _) =&gt; raise DLError msg
          val _ = sml_register_stackmap (smap, base)
          val ptr = DynamicLink.dlsym (so, mainSymbol)
                    handle OS.SysErr (msg, _) =&gt; raise DLError msg
          (*
           * Note that &quot;ptr&quot; points to an ML toplevel code. This toplevel code
           * should be called by the calling convention for ML toplevels of
           * ML object files.  __attribute__((fastcc,no_callback)) is an ad
           * hoc way of yielding this convention code; no_callback avoids
           * calling sml_control_suspend.  If we change how to compile
           * attributes in the future, we should revisit here and update the
           * __attribute__ annotation.
           *)
          val mainFn =
              ptr : _import __attribute__((fastcc,no_callback)) () -&gt; ()
        in
          loadedFiles := sofile :: !loadedFiles;
          mainFn () handle e =&gt; raise UncaughtException e;
          SUCCESS newContext
        end
        handle e =&gt;
          (
            case e of
              UserError.UserErrors errs =&gt;
              app (fn e =&gt; puts (userErrorToString e)) errs
            | UserError.UserErrorsWithoutLoc errs =&gt;
              app (fn (k,e) =&gt; puts (userErrorToString (Loc.noloc,k,e))) errs
            | DLError s =&gt;
              puts (&quot;failed dynamic linking. Perhaps incorrect name in _import declaration: &quot; ^ s)
            | UncaughtException exn =&gt;
              puts (&quot;uncaught exception &quot; ^ exnMessage exn)
            | CoreUtils.Failed {command, message} =&gt;
              (puts (&quot;command failed: &quot; ^ command); puts message)
            | _ =&gt; raise e;
            FAILED
          )
      end
      handle CompileError =&gt; FAILED
</code></pre>

<p>デカいですね。どうやら<code>run</code>がREPLの本体のようです。少しずつ見ていきます。</p>

<p>まずはコンパイルしているようです。</p>

<pre><code class="language-sml">        val ({interfaceNameOpt, ...}, result) =
             Top.compile options context input
</code></pre>

<p>そして結果からcontextとmoduleを取り出します。</p>

<pre><code class="language-sml">        val (newContext, module) =
            case result of
              Top.RETURN (newContext, module) =&gt; (newContext, module)
            | Top.STOPPED =&gt; raise Bug.Bug &quot;run&quot;
</code></pre>

<p>恐らくcontextが変数名とかを持っているのでしょう。
moduleに関しては<code>Top.compile</code>が最後にLLVMEmitをしてるのでLLVMのmodule(コンパイルされる最大単位)のことだと思います。だとするとここまではまだコードはメモリ上にあるはずです。</p>

<p>moduleを取り出した後どうしてるかというとオブジェクトファイルを作っているようです。ここでファイルに書き出されました。</p>

<pre><code>          val objfile = TempFile.create (&quot;.&quot; ^ SMLSharp_Config.OBJEXT ())
          val asmfile = TempFile.create (&quot;.&quot; ^ SMLSharp_Config.ASMEXT ())
          val _ = #start Counter.llvmOutputTimeCounter()
          val _ = LLVM.compile llvmOptions (module, LLVM.AssemblyFile,
                                            Filename.toString asmfile)
          val _ = LLVM.compile llvmOptions (module, LLVM.ObjectFile,
                                            Filename.toString objfile)
          val _ = #stop Counter.llvmOutputTimeCounter()
          val _ = LLVM.LLVMDisposeModule module
</code></pre>

<p>最後に呼んでるDisposeModuleはデストラクタですかね。ちゃんとお片付けしてます。</p>

<p>さらに、ダイナミックリンクファイルを作るようです。</p>

<pre><code class="language-sml">          val sofile = TempFile.create (SMLSharp_Config.DLLEXT ())
          val ldflags =
              case SMLSharp_Config.HOST_OS_TYPE () of
                SMLSharp_Config.Unix =&gt; nil
              | SMLSharp_Config.Cygwin =&gt;
                [&quot;-Wl,-out-implib,&quot;
                 ^ Filename.toString (Filename.replaceSuffix &quot;lib&quot; sofile)]
              | SMLSharp_Config.Mingw =&gt;
                [&quot;-Wl,--out-implib=&quot;
                 ^ Filename.toString (Filename.replaceSuffix &quot;lib&quot; sofile)]
          val libfiles =
              case SMLSharp_Config.HOST_OS_TYPE () of
                SMLSharp_Config.Unix =&gt; nil
              | SMLSharp_Config.Cygwin =&gt;
                map (fn x =&gt; Filename.toString (Filename.replaceSuffix &quot;lib&quot; x))
                    (!loadedFiles)
              | SMLSharp_Config.Mingw =&gt;
                map (fn x =&gt; Filename.toString (Filename.replaceSuffix &quot;lib&quot; x))
                    (!loadedFiles)
          val _ = BinUtils.link
                    {flags = SMLSharp_Config.RUNLOOP_DLDFLAGS () :: LDFLAGS
                             @ ldflags,
                     libs = libfiles @ LIBS,
                     objects = [objfile],
                     dst = sofile,
                     useCXX = false,
                     quiet = not (!Control.printCommand)}
</code></pre>

<p>この時、Windowsでは何かしらの制約があるのか過去にリンクしたダイナミックリンクファイル(libfiles)も今回作るダイナミックリンクファイルにリンクしているようです。</p>

<p>さて、ダイナミックリンクファイルを作った後は<code>dlopen</code>を使ってダイナミックリンクします。</p>

<pre><code class="language-sml">          val so = DynamicLink.dlopen' (Filename.toString sofile,
                                        DynamicLink.GLOBAL,
                                        DynamicLink.NOW)
                   handle OS.SysErr (msg, _) =&gt; raise DLError msg
</code></pre>

<p>その後はエントリポイントを捜してインポート、</p>

<pre><code class="language-sml">          val {mainSymbol, stackMapSymbol, codeBeginSymbol, ...} =
              GenerateMain.moduleName (interfaceNameOpt, #version context)
          val smap = DynamicLink.dlsym' (so, stackMapSymbol)
                     handle OS.SysErr (msg, _) =&gt; raise DLError msg
          val base = DynamicLink.dlsym' (so, codeBeginSymbol)
                     handle OS.SysErr (msg, _) =&gt; raise DLError msg
          val _ = sml_register_stackmap (smap, base)
          val ptr = DynamicLink.dlsym (so, mainSymbol)
                    handle OS.SysErr (msg, _) =&gt; raise DLError msg
</code></pre>

<p>そして実行します。</p>

<pre><code class="language-sml">          val mainFn =
              ptr : _import __attribute__((fastcc,no_callback)) () -&gt; ()
        in
          loadedFiles := sofile :: !loadedFiles;
          mainFn () handle e =&gt; raise UncaughtException e;
          SUCCESS newContext
</code></pre>

<p>結果を印字しているところが見当らないのですが吐かれたコードに印字部分も入っているんですかね。</p>

<p>あとは<code>interactive</code>に返ってコンテキストを拡張したりバージョンをインクリメントしたりします。</p>

<pre><code class="language-sml">                   val context = Top.extendContext (context, newContext)
                   val context = Top.incVersion context
</code></pre>

<p>バージョンによってシンボル名を変えることで何度もsoファイルをロードしても大丈夫なようにしてるんですかね。</p>

<p>ということでREPLの実装は入力を一旦ダイナミックリンクとしてコンパイルしてからロードしているようでした。</p>

<h1 id="おまけ">おまけ</h1>

<p>REPLの入力部分は次のようになっています。</p>

<pre><code class="language-sml">
  fun initInteractive () =
      let
        val lineCount = ref 1
        val eof = ref false
        fun read (isFirst, _:int) =
            let
              val prompt = if isFirst then &quot;# &quot; else &quot;&gt; &quot;
              val _ = TextIO.output (TextIO.stdOut, prompt)
              val _ = TextIO.flushOut TextIO.stdOut
              val line = TextIO.inputLine TextIO.stdIn
              val _ = lineCount := !lineCount + 1
            in
              case line of NONE =&gt; (eof := true; &quot;&quot;) | SOME s =&gt; s
            end
      in
        {lineCount = lineCount, eof = eof, read = read}
      end

</code></pre>

<p>結構小さいので色々Hack出来ます。例えば<a href="https://github.com/smlsharp/smlsharp/pull/13">hskさんのように</a>REPLを付けることも出来ます。</p>

<p>このパッチで外部コマンドを呼ぶのが少し嫌な人はさらに<code>ioctl(2)</code>を使ってrawモードにするパッチを当てるかlibeditなんかを使ってリッチな入力を実現するかをすればいいでしょう。
個人利用なら(他人に配布しないなら)GPLv3のreadlineも使えるんですかね（ライセンスに詳しくない）。</p>

<h1 id="まとめ">まとめ</h1>

<ul>
<li>REPLの実装にはダイナミックリンクを使う方法があるよ</li>
<li>SML#のREPLはHack出来そうだよ</li>
</ul>
                    </section>
                </article>
            </div>
        </div>

        <div class="row li-author">
    <div class="sixteen columns">
        Written by <strong>κeen</strong>
    </div>
</div>


        <div class="row li-pagination">
            <div class="eight columns">
                <div class="li-pagination-previous">
                    
                        Later article<br />
                        <a href="/blog/2015/12/13/rustdenonihongonoatsukai"> Rustでの日本語の扱い</a>
                    
                </div>
            </div>
            <div class="eight columns">
                <div class="li-pagination-next">
                    
                        Older article<br />
                        <a href="/blog/2015/12/12/sml_dejitwotsukurukaruihanashi"> SML#でJITコンパイラを作る軽い話</a>
                    
                </div>
            </div>
        </div>
    </div>

<footer class="li-page-footer">
    <div class="container">
        <div class="row">
            <div class="sixteen columns">
                <div class="li-page-footer-legal">
                    &copy; 2016. All rights reserved. 
                </div>
                <div class="li-page-footer-theme">
                    <span class=""><a href="https://github.com/eliasson/liquorice/">liquorice</a> is a theme for <a href="http://hugo.spf13.com">hugo</a></span>
                </div>
            </div>
        </div>
    </div>
</footer>

    <script type="text/javascript">
    <!--
    function toggle(id) {
        var e = document.getElementById(id);
        e.style.display == 'block' ? e.style.display = 'none' : e.style.display = 'block';
    }
    
    </script>
    <script type="text/javascript">
    <!--
        var _gaq = _gaq || [];
        _gaq.push(['_setAccount', ""]);
        _gaq.push(['_trackPageview']);

        (function() {
        var ga = document.createElement('script'); ga.type = 'text/javascript'; ga.async = true;
        ga.src = ('https:' == document.location.protocol ? 'https://ssl' : 'http://www') + '.google-analytics.com/ga.js';
        var s = document.getElementsByTagName('script')[0]; s.parentNode.insertBefore(ga, s);
        })();
    -->
    </script>
    </body>
</html>

